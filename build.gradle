plugins {
  id 'groovy'
  id 'codenarc'
  id 'application'
  id 'maven'
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.7.3'
}

archivesBaseName = name
mainClassName = 'gcolors.Colors'

configurations {
  asciidoclet
}

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  compile "org.codehaus.groovy:groovy:${groovyVersion}"
  testCompile("org.spockframework:spock-core:${spockVersion}") {
    exclude module: 'groovy-all'
  }
  asciidoclet "org.asciidoctor:asciidoclet:${asciidocletVersion}"
}

codenarc {
  configFile             = file "${project.projectDir}/config/codenarc.groovy"
  ignoreFailures         = false
  reportFormat           = 'html'
  toolVersion            = codenarcVersion
  maxPriority1Violations = 0
  maxPriority2Violations = 0
  maxPriority3Violations = 0
}

javadoc {
  options.docletpath = configurations.asciidoclet.files.asType(List)
  options.doclet = 'org.asciidoctor.Asciidoclet'
}

tasks.withType(Groovydoc) {
  docTitle    = "Groovy API Documentation for for gcolors ${version}"
  windowTitle = "Groovy API for gcolors ${version}"
  footer      = "<p style='text-align: center'>Groovy API Documentation for gcolors ${version} - Licensed under the Apache License, Version 2.0 - <a href='https://github.com/david-w-millar/gcolors/'>gcolors</a></p>"
}

artifacts { archives jar }

bintray {
  user           = System.getenv('BINTRAY_USER')
  key            = System.getenv('BINTRAY_KEY')
  configurations = ["archives"]
  pkg {
    repo     = "maven"
    name     = project.name
    licenses = ["Apache-2.0"]
    vcsUrl   = "https://github.com/david-w-millar/gcolors"
    version {
      name     = project.version
      vcsTag   = project.version
      desc     = project.description
      released = new Date()
    }
  }
}

publishing {
    publications {
        GColors(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}

